{
    function makeInteger(o) {
        return parseInt(o.join(""), 10);
    }
}

pgn
    = pw:pgnStartWhite all:pgnBlack? { arr = (all ? all : []); arr.push(pw); arr.reverse(); return arr; }
/ pb:pgnStartBlack all:pgnWhite? { arr = (all ? all : []); arr.push(pb); arr.reverse(); return arr; }
/ whiteSpace? { return [[]]; }

pgnStartWhite = whiteSpace? pw:pgnWhite { return pw; }

pgnStartBlack = whiteSpace? me:moveEllipse all:pgnBlack { last = all[all.length - 1]; last.moveNumber = me; return all; }

pgnWhite = whiteSpace? mn:moveNumber whiteSpace? cb:comment? whiteSpace? hm:halfMove  whiteSpace? ca:comment? whiteSpace? all:pgnBlack? { arr = (all ? all : []); move = {}; move.turn = 'w'; move.moveNumber = mn; move.notation = hm; move.commentBefore = cb; move.commentAfter = ca; arr.push(move); return arr; }

pgnBlack = whiteSpace? cb:comment? whiteSpace? hm:halfMove whiteSpace? ca:comment? whiteSpace? all:pgnWhite? { arr = (all ? all : []); move = {}; move.turn = 'b', move.notation = hm; move.commentBefore = cb; move.commentAfter = ca; arr.push(move); return arr; }

comment
  = cl cm:[^}]+ cr { return cm.join("").trim(); }

cl = '{'

cr = '}'


moveNumber
    = num:integer"." { return num; }

integer "integer"
    = digits:[0-9]+ { return makeInteger(digits); }

whiteSpace
    = " "+ { return '';}

halfMove
    = fig:figure? & checkdisc disc:discriminator str:strike? col:column row:row pr:promotion? ch:check? {return (fig ? fig : '') + (disc ? disc : '') + (str ? str : '') + col + row + (ch ? ch : ''); }
/ fig:figure? str:strike? col:column row:row pr:promotion? ch:check? {return (fig ? fig : '') + (str ? str : '') + col + row + (pr ? pr : '') +  (ch ? ch : ''); }
/ 'O-O-O'
/ 'O-O'

check = '+'
  / '#'

promotion
  = '=' f:figure { return '=' + f; }


discriminator
    = column
    / row

checkdisc
    = discriminator strike? column row

moveEllipse
    = num:integer"..." { return num; }

figure
    = [RNBQK]

column
    = [a-h]

row
    = [1-8]

strike
    = 'x'